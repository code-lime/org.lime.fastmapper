plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.papermc.paperweight.userdev" version "+"
    id "fr.brouillard.oss.gradle.jgitver" version "+"
    id 'com.google.protobuf' version "${versionProtobufGradle}"
}

//<editor-fold desc="Settings: Plugin">
import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

dependencies {
    paperweight.paperDevBundle("${versionPaper}")
}

apply from: 'https://raw.githubusercontent.com/code-lime/org.lime.core/refs/tags/2.0.14/global.gradle'
apply from: 'https://raw.githubusercontent.com/code-lime/org.lime.core/refs/tags/2.0.14/version.gradle'

println "Project version: $version"

paperweight.reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

processResources {
    configureProcessResources(delegate, "plugin.yml", [
            group: "${project.group}",
    ])
}
//</editor-fold>

//<editor-fold desc="Settings: Grpc">
dependencies {
    implementation("io.grpc:grpc-protobuf:${versionGrpc}") {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    implementation "io.grpc:grpc-stub:${versionGrpc}"
    implementation "io.grpc:grpc-auth:${versionGrpc}"
    implementation "io.grpc:grpc-netty:${versionGrpc}"

    implementation "com.google.protobuf:protobuf-java:${versionProtobuf}"

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${versionProtobuf}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${versionGrpc}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
//</editor-fold>

//<editor-fold desc="Settings: Test">
dependencies {
    testImplementation(platform('org.junit:junit-bom:5.12.1'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.12.1')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher:1.12.1')
}

test {
    useJUnitPlatform()
    jvmArgs(
            '-Djdk.reflect.useDirectMethodHandle=false',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
    )
    testLogging {
        events "passed", "skipped", "failed"
    }
}
//</editor-fold>

//<editor-fold desc="Settings: Maven">
publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components["java"])
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("GITHUB_OWNER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
//</editor-fold>

//<editor-fold desc="Settings: Libraries">
//<editor-fold desc="Settings: Libraries: org.lime.core">
repositories {
    maven {
        url = "https://maven.pkg.github.com/code-lime/org.lime.core"
        credentials {
            username = System.getenv("GITHUB_PACKAGE_USERNAME")
            password = System.getenv("GITHUB_PACKAGE_TOKEN")
        }
    }
}

dependencies {
    implementation "org.lime:core:${versionCore}"
}
//</editor-fold>
//</editor-fold>